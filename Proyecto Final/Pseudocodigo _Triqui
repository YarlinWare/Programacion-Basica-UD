Subproceso cuadricula(triqui)
	Borrar  Pantalla;
	Escribir "    _____ ____  ___ ___  _   _ ___           _   _ ____    ____   ___  _  ___  ";
	Escribir "   |_   _|  _ \|_ _/ _ \| | | |_ _|         | | | |  _ \  |___ \ / _ \/ |( _ ) ";
	Escribir "     | | | |_) || | | | | | | || |   _____  | | | | | | |   __) | | | | |/ _ \ ";
	Escribir "     | | |  _ < | | |_| | |_| || |  |_____| | |_| | |_| |  / __/| |_| | | (_) |";
	Escribir "     |_| |_| \_\___\__\_\\___/|___|          \___/|____/  |_____|\___/|_|\___/ ";
	Escribir " ";
	Escribir " ";

	Escribir " Ejemplo posiciones: ";
	Escribir " ";
	Escribir "      ----------------";
	Escribir "      | 11 | 12 | 13 |";
	Escribir "      ----------------";
	Escribir "      | 21 | 22 | 23 |";
	Escribir "      ----------------";
	Escribir "      | 31 | 32 | 33 |";
	Escribir "      ----------------";
	Escribir "";

	Escribir "      -------------";
	Para i<- 1 hasta 3 con paso 1 Hacer
		si i<> 1 Entonces
			Escribir "      -------------";
		FinSi
		Escribir  Sin saltar "      |";
		Para j<- 1 Hasta 3 con paso 1 Hacer
			Escribir Sin saltar " ", triqui[i,j]," |";
		FinPara
		Escribir "";
	FinPara
	Escribir "      -------------";
	Escribir "";
FinSubProceso	

Subproceso numero <- leernumero(variable)
	Repetir
		Escribir sin saltar "Seleccion el numero de ", variable, ":";
		leer numero;
		si numero<1 o numero>3 Entonces
			Escribir "Numero no valido";
		FinSi
	Hasta Que numero=1 o numero=2 o numero=3;
FinSubProceso

Proceso  pseudocodigotriqui
	Dimension triqui[3,3];
	para i<- 1 hasta 3 con paso 1 Hacer
		para j<- 1 hasta 3 con paso 1 Hacer
			triqui[i,j] <- ' ';
		FinPara
	FinPara
	
	Ganador <- ' ';
	desicion <- azar(2)=1
	turno <- 0;
	
	Mientras turno < 9 y Ganador <>'O' y Ganador <> 'X' Hacer
		si desicion Entonces
			Repetir
				i <- 1+azar(3);
				j <- 1+azar(3);
			Hasta que triqui[i,j] = ' ';
			triqui[i,j] <- 'O';
		Sino
			cuadricula(triqui);
			Repetir
				i <- leernumero("fila");
				j <- leernumero("columna");
				si triqui[i,j] <> ' ' Entonces
					Escribir "La casilla seleccionada esta ocupada.";
					Escribir "";
				FinSi
			Hasta que triqui[i,j] = ' ';
			triqui[i,j] <- 'X';
		FinSi
		i <- 1;
		mientras Ganador = ' ' Y i <= 3 Hacer
			si triqui[i,1] <> ' ' y triqui[i,1] = triqui[i,2] y triqui[i,2] = triqui[i,3] Entonces
				Ganador <- triqui [i,1]
			Finsi
			i <- i + 1;
		FinMientras
		i <- 1;
		mientras Ganador = ' ' Y i <= 3 Hacer
			si triqui[1,i] <> ' ' y triqui[1,i] = triqui[2,i] y triqui[2,i] = triqui[3,i] Entonces
				Ganador <- triqui [1,1];
			FinSi
			i <- i + 1;
		FinMientras
		Si Ganador = ' ' y triqui[1,1] <> ' ' y triqui[1,1] = triqui[2,2] y triqui[2,2] = triqui[3,3] Entonces
			Ganador <- triqui[1,1];
		FinSi
		si Ganador = ' ' y triqui[1,3] <> ' ' y triqui[1,3] = triqui[2,2] y triqui[2,2] = triqui[3,1] Entonces
			Ganador <- triqui[1,3];
		FinSi
		desicion <- No desicion;
		turno <- turno + 1;
	FinMientras
	
	cuadricula(triqui);
	segun Ganador Hacer
		'O':
			Escribir "La computadora gano.";
		'X':
			Escribir "El Usuario gano.";
		' ':
			Escribir "Empate.";
	FinSegun
FinProceso	